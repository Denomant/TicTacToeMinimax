plugins {
    id 'java'
    id 'application'
}

version = '1.0'

application {
    mainClass = 'App'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'lib', include: ['*.jar'])
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// Jar task to create a fat jar including all dependencies
jar {
    manifest {
        attributes (
            'Main-Class': 'App',
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        // remove signature files and other manifests from dependency jars
        exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.EC', 'META-INF/MANIFEST.MF'
    }
}

tasks.named('run', JavaExec).configure {
    // forward the terminal stdin to the Java process so interactive input works
    standardInput = System.in
}